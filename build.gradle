plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'groovy'
    id 'signing'
}

group 'org.m2ci.msp'
version '0.7-SNAPSHOT'
description 'Gradle Praat wrapper plugin'

ext {
    isReleaseVersion = !version.endsWith('-SNAPSHOT')
    url = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    praatVersion = '6.0.30'
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    description = 'Praat provider for Gradle'
    tags = ['phonetics', 'speech science']
}

gradlePlugin {
    plugins {
        praatWrapperPlugin {
            id = 'org.m2ci.msp.praat-wrapper'
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
            displayName = project.description
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    expand praatVersion: praatVersion
}

test {
    useTestNG()
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    publications {
        withType(MavenPublication) {
            pom {
                name = 'Gradle Praat wrapper plugin'
                description = project.description
                url = project.url
                inceptionYear = '2017'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0-standalone.html'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        url = 'https://github.com/psibre'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/m2ci-msp/gradle-praat-wrapper-plugin.git'
                    developerConnection = 'scm:git:git@github.com/m2ci-msp/gradle-praat-wrapper-plugin.git'
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'NewOSSRH'
            url isReleaseVersion ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
                    : 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
