plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'groovy'
    id 'signing'
}

group 'io.github.m2ci-msp'
version '0.8.0-SNAPSHOT'
description 'Gradle Praat wrapper plugin'

ext {
    isReleaseVersion = !version.endsWith('-SNAPSHOT')
    url = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    praatVersion = '6.0.30'
}

gradlePlugin {
    def url = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    website = url
    vcsUrl = url
    plugins {
        praatWrapperPlugin {
            id = 'io.github.m2ci-msp.praat-wrapper'
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
            displayName = project.description
            description = 'Praat provider for Gradle'
            tags.set(['phonetics', 'speech science'])
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.testng', name: 'testng', version: '7.5.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    expand praatVersion: praatVersion
}

test {
    useTestNG()
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    publications {
        withType(MavenPublication) {
            pom {
                name = 'Gradle Praat wrapper plugin'
                description = project.description
                url = project.url
                inceptionYear = '2017'
                licenses {
                    license {
                        name = 'GNU General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0-standalone.html'
                    }
                }
                developers {
                    developer {
                        name = 'Ingmar Steiner'
                        url = 'https://github.com/psibre'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/m2ci-msp/gradle-praat-wrapper-plugin.git'
                    developerConnection = 'scm:git:git@github.com/m2ci-msp/gradle-praat-wrapper-plugin.git'
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            name 'NewOSSRH'
            url isReleaseVersion ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
                    : 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
