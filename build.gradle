buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
}
apply plugin: 'com.gradle.plugin-publish'

ext {
    pluginId = 'org.m2ci.msp.praat-wrapper'
    praatVersion = '5.4.17'
}

gradlePlugin {
    plugins {
        praatWrapperPlugin {
            id = pluginId
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
        }
    }
}

group 'org.m2ci.msp'
version '0.5-SNAPSHOT'

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    testCompile group: 'org.testng', name: 'testng', version: '6.9.10'
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    include '**/build.gradle'
    switch (org.gradle.internal.os.OperatingSystem.current()) {
        case { it.isMacOsX() }:
            include '**/script_mac.praat'
            break
        case { it.isLinux() }:
            include '**/script_linux.praat'
            break
        case { it.isWindows() }:
            include '**/script_windows.praat'
            break
    }
    filesMatching('**/script_*.praat') {
        it.name = 'script.praat'
    }
    expand praatVersion: praatVersion
}

test {
    useTestNG()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    description = 'Praat provider for Gradle'
    tags = ['phonetics', 'speech science']

    plugins {
        praatWrapperPlugin {
            id = pluginId
            displayName = 'Gradle Praat wrapper plugin'
        }
    }
}
