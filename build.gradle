buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'com.gradle.publish:plugin-publish-plugin:0.9.9'
    }
}

plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
}

apply plugin: 'com.gradle.plugin-publish'

targetCompatibility = 1.7

ext {
    pluginId = 'org.m2ci.msp.praat-wrapper'
    praatVersion = '5.4.17'
}

gradlePlugin {
    plugins {
        praatWrapperPlugin {
            id = pluginId
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
        }
    }
}

group 'org.m2ci.msp'
version '0.5'

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    testCompile group: 'org.testng', name: 'testng', version: '6.11'
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    expand praatVersion: praatVersion
}

test {
    useTestNG()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
        }
    }
    repositories {
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/m2ci-msp/maven/gradle-praat-plugin'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    description = 'Praat provider for Gradle'
    tags = ['phonetics', 'speech science']

    plugins {
        praatWrapperPlugin {
            id = pluginId
            displayName = 'Gradle Praat wrapper plugin'
        }
    }
}
