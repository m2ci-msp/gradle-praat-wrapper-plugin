plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.11.0'
}

targetCompatibility = 1.8

ext {
    praatVersion = '6.0.30'
}

gradlePlugin {
    plugins {
        praatWrapperPlugin {
            id = 'org.m2ci.msp.praat-wrapper'
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
        }
    }
}

group 'org.m2ci.msp'
version '0.6'

repositories {
    jcenter()
}

dependencies {
    implementation localGroovy()
    testImplementation group: 'org.testng', name: 'testng', version: '7.1.0'
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    expand praatVersion: praatVersion
}

java {
    withSourcesJar()
}

test {
    useTestNG()
    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url version.endsWith('-SNAPSHOT') ? 'https://oss.jfrog.org/artifactory/oss-snapshot-local' : 'https://api.bintray.com/maven/m2ci-msp/maven/gradle-praat-plugin'
            credentials {
                username = findProperty('bintrayUser')
                password = findProperty('bintrayApiKey')
            }
        }
    }
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    description = 'Praat provider for Gradle'
    tags = ['phonetics', 'speech science']

    plugins {
        praatWrapperPlugin {
            displayName = 'Gradle Praat wrapper plugin'
        }
    }
}
