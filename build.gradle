plugins {
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'groovy'
    id 'signing'
}

group 'org.m2ci.msp'
version '0.7-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith('-SNAPSHOT')

ext {
    praatVersion = '6.0.30'
}

pluginBundle {
    website = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    vcsUrl = 'https://github.com/m2ci-msp/gradle-praat-wrapper-plugin'
    description = 'Praat provider for Gradle'
    tags = ['phonetics', 'speech science']
}

gradlePlugin {
    plugins {
        praatWrapperPlugin {
            id = 'org.m2ci.msp.praat-wrapper'
            implementationClass = 'org.m2ci.msp.praat.PraatWrapperPlugin'
            displayName = 'Gradle Praat wrapper plugin'
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.testng', name: 'testng', version: '7.5'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withJavadocJar()
    withSourcesJar()
}

processResources {
    expand praatVersion: praatVersion
}

processTestResources {
    expand praatVersion: praatVersion
}

test {
    useTestNG()
    testLogging {
        events 'passed', 'failed', 'skipped'
        exceptionFormat = 'full'
        info {
            showStandardStreams = true
        }
    }
}

publishing {
    repositories {
        maven {
            name 'NewOSSRH'
            url isReleaseVersion ? 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
                    : 'https://s01.oss.sonatype.org/content/repositories/snapshots'
            credentials(PasswordCredentials)
        }
    }
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}
