subprojects {
    repositories {
        def praatVersionNoDots = praatVersion.replaceAll(~/\./, '')
        ivy {
            url 'http://www.fon.hum.uva.nl/praat/old/'
            layout 'pattern', {
                artifact "$praatVersionNoDots/[module]${praatVersionNoDots}_[classifier].[ext]"
            }
        }
    }

    configurations {
        praat
    }

    dependencies {
        compile group: "gradle.plugin.$group", name: 'praat-wrapper', version: version
        testCompile project(':praat-wrapper').sourceSets.test.output
        praat group: 'org.praat', name: praatName, version: praatVersion, classifier: praatClassifier, ext: praatExtension
    }

    configurations.all {
        resolutionStrategy.dependencySubstitution {
            substitute module("gradle.plugin.$group:praat-wrapper") with project(':praat-wrapper')
        }
    }

    sourceSets {
        test {
            groovy {
                srcDirs "$rootDir/sharedSrc/test/groovy"
            }
        }
    }

    task copyPraatDependency(type: Copy) {
        from configurations.praat
        into temporaryDir
        processResources.dependsOn it
    }

    processTestResources {
        expand praatVersion: praatVersion
    }
}

processResources {
    expand praatVersion: praatVersion
}
